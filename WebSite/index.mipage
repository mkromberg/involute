:Class index : MiPage

    ∇ Compose;input;inputArea;outputArea
      :Access Public
      Add _.Style'#num,#dir'('border-style' 'solid' 'margin' '0.5em' 'padding' '10px')  
      Add _.title 'IUI - The Involution UI'
     
     ⍝input←('N'('value' 1))New _.EditField''
      input←('N'('type' 'number')('min' 1)('max' 20)('value' 1))New _.EditField''
     ⍝input←('N'('type' 'range')('min' 1)('max' 20)('value' 1))New _.EditField''
     
      inputArea←New _.InputGrid'N:'input
      (outputArea←New _.StackPanel).Add¨'#num' '#dir'{⍺ New _.pre ⍵}¨1 '→'
      outputArea.Horizontal←1
      (Add _.StackPanel).Add¨inputArea outputArea
     
      input.On'change' 'Callback'
    ∇

    ∇ r←Callback
      :Access Public
      :If (,83)≡⎕DR¨N←#.Strings.tonum _value ⍝ single integer?
          r←⊃,/'#num' '#dir'Replace¨0 1 involute¨N
      :Else
          r←Execute _JSS.Alert'Please enter a single integer'
      :EndIf
    ∇

      involute←{
          ⍺←0
          repeat←1↓2/⌽⍳⍵
          moves←(¯1+2×⍵)⍴m←(⊢,-)1 ⍵
          path←+\repeat/moves
          ⍺≡0:⍵ ⍵⍴⍋path
          r←⍵ ⍵⍴((repeat/'→↓←↑'[m⍳moves])@path)(⍵×⍵)⍴⍺
          ((2×⍵)⍴1 0)\r
      }

:EndClass
